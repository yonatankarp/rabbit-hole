plugins {
    id "java"
    id "jacoco"
    id "org.jetbrains.kotlin.jvm"
    id "org.jetbrains.kotlin.plugin.spring"
    id "org.springframework.boot"
    id "io.spring.dependency-management"
    id "com.diffplug.spotless"
}

group = 'com.yonatankarp'
version "${version}"
sourceCompatibility = '11'

jar {
    enabled = false
}

bootJar {
    enabled = true
}

repositories {
    mavenLocal()
    mavenCentral()
    maven {
        name = "Rabbit Hole"
        url = uri("https://maven.pkg.github.com/yonatankarp/rabbit-hole")
        credentials {
            username = project.findProperty("gpr.user") ?: System.getenv("GITHUB_ACTOR")
            password = project.findProperty("gpr.key") ?: System.getenv('GITHUB_TOKEN')
        }
    }
}

dependencies {
    implementation group: "org.jetbrains.kotlin", name: "kotlin-stdlib-jdk8"
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-amqp'

    // Enable this to consume the published jar from GitHub packages
    implementation "com.yonatankarp:rabbit-hole:+"
    // Enable this to consume the local Jar from the project
//    implementation(project(":rabbit-hole"))

    testImplementation("org.springframework.boot:spring-boot-starter-test") {
        exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
    }
}

build {
    finalizedBy spotlessApply
}

test {
    useJUnitPlatform()
    finalizedBy spotlessApply
    finalizedBy jacocoTestReport
}

allOpen {
    annotation("org.springframework.context.annotation.Configuration")
}
