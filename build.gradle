plugins {
	id 'java'
	id 'idea'
	id 'maven-publish'
	id 'org.springframework.boot' version '2.4.4'
	id 'io.spring.dependency-management' version '1.0.11.RELEASE'
	id 'com.diffplug.spotless' version '5.9.0'
}

ext {
	springBootVersion = '2.4.4'
}

apply from: 'codemetrics.gradle'

group = 'com.yonatankarp'
version "${version}"
sourceCompatibility = '11'

repositories {
	mavenCentral()
}

jar {
	enabled = true
	archivesBaseName = project.name
	version = project.version
}

dependencies {
	compile "org.springframework.boot:spring-boot-starter-amqp:$springBootVersion"

	testImplementation "org.springframework.boot:spring-boot-starter-test:$springBootVersion"
}

test {
	useJUnitPlatform()
	finalizedBy spotlessApply
	finalizedBy jacocoTestReport
}

publishing {
	repositories {
		maven {
			name = "GitHubPackages"
			url = uri("https://maven.pkg.github.com/yonatankarp/rabbit-hole")
			credentials {
				username = project.findProperty("gpr.user") ?: System.getenv("GITHUB_ACTOR")
				password = project.findProperty("gpr.key") ?: System.getenv("GITHUB_TOKEN")
			}
		}
	}
	publications {
		gpr(MavenPublication) {
			artifact bootJar
			versionMapping {
				usage('java-runtime') {
					fromResolutionResult()
				}
			}
		}
		maven(MavenPublication) {
			artifact bootJar
			versionMapping {
				usage('java-runtime') {
					fromResolutionResult()
				}
			}
		}
	}
}
