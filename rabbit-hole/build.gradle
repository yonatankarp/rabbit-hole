plugins {
    id "java"
    id "org.jetbrains.kotlin.jvm"
    id "org.jetbrains.kotlin.plugin.allopen"
    id "jacoco"
    id "maven-publish"
    id "org.springframework.boot"
    id "io.spring.dependency-management"
    id "com.diffplug.spotless"
}

jar {
    enabled = true
    archiveClassifier = ''
}

bootJar {
    enabled = false
}

dependencies {
    implementation group: "org.jetbrains.kotlin", name: "kotlin-stdlib-jdk8"
    implementation group: 'org.springframework.boot', name: 'spring-boot-starter-amqp'

    // Tests
    testImplementation("org.springframework.boot:spring-boot-starter-test") {
        exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
    }
    testImplementation group: 'io.mockk', name: 'mockk', version: '1.12.2'

}

build {
    finalizedBy spotlessApply
}

test {
    useJUnitPlatform()
    finalizedBy spotlessApply
    finalizedBy jacocoTestReport
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId = 'com.yonatankarp'
            artifactId = project.name
            version = project.version

            from components.java
            versionMapping {
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }
        }
    }

    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/yonatankarp/rabbit-hole")
            credentials {
                username = project.findProperty("gpr.user") ?: System.getenv("GITHUB_ACTOR")
                password = project.findProperty("gpr.key") ?: System.getenv("GITHUB_TOKEN")
            }
        }
    }
}

allOpen {
    annotation("org.springframework.context.annotation.Configuration")
}
